if (isLoadUiComponent.toBoolean()) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
//    buildToolsVersion rootProject.ext.android.buildToolsVersion


    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        if (!isLoadUiComponent.toBoolean()) {
            applicationId "com.friean.uicomponent"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    if (!isLoadUiComponent.toBoolean()) {
        sourceSets {
            main {
                manifest.srcFile 'src/main/developconfig/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation rootProject.ext.dependencies.libAndroidSupportV7
    implementation rootProject.ext.dependencies.libConstraintLayout
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation rootProject.ext.dependencies.libjunit
    androidTestImplementation rootProject.ext.dependencies.libTestRunner
    androidTestImplementation rootProject.ext.dependencies.libTestEspresso
    implementation 'androidx.recyclerview:recyclerview:1.1.0-beta05'
    implementation 'com.google.android.material:material:1.1.0'
}
